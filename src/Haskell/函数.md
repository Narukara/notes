## 函数

```haskell
-- 定义
a = "hello"		-- 没有参数的函数，也称作定义，值不可修改
doubleMe x = x + x
doubleUs x y = x*2 + y*2

-- 调用
doubleMe 3
doubleUs 3 4
3 `doubleUs` 4	-- 前缀函数以中缀形式调用，凡是有两个参数的函数都可以这样做，定义函数时也可以用
(+) 3 4			-- 中缀函数以前缀形式调用
```

函数调用具有最高的优先级

函数名里可以加单引号，没有任何特殊意义。

---

## 模式匹配

```haskell
factorial :: (Integral a) => a -> a   
factorial 0 = 1   
factorial n = n * factorial (n - 1)  
```

对 Tuple 使用

```haskell
addVectors :: (Num a) => (a, a) -> (a, a) -> (a, a)   
addVectors (x1, y1) (x2, y2) = (x1 + x2, y1 + y2)

third :: (a, b, c) -> c   
third (_, _, z) = z  
```

对 List 使用

```haskell
tell :: (Show a) => [a] -> String
tell [] = "The list is empty"
tell (x:[]) = "The list has one element: " ++ show x
tell (x:y:[]) = "The list has two elements: " ++ show x ++ " and " ++ show y
tell (x:y:_) = "This list is long. The first two elements are: " ++ show x ++ " and " ++ show y
```

as模式：在按模式分割东西时仍保留对其整体的引用

```haskell
capital :: String -> String
capital "" = "Empty string, whoops!"
capital all@(x:xs) = "The first letter of " ++ all ++ " is " ++ [x]
```

## Guard

```haskell
bmiTell :: (RealFloat a) => a -> String  
bmiTell bmi  
    | bmi <= 18.5 = "You're underweight, you emo, you!"  
    | bmi <= 25.0 = "You're supposedly normal. Pffft, I bet you're ugly!"  
    | bmi <= 30.0 = "You're fat! Lose some weight, fatty!"  
    | otherwise   = "You're a whale, congratulations!"
```

模式匹配和Guard可以混合使用

### where

```haskell
bmiTell :: (RealFloat a) => a -> a -> String
bmiTell weight height
    | bmi <= skinny = "You're underweight, you emo, you!"
    | bmi <= normal = "You're supposedly normal. Pffft, I bet you're ugly!"
    | bmi <= fat    = "You're fat! Lose some weight, fatty!"
    | otherwise     = "You're a whale, congratulations!"
    where bmi = weight / height ^ 2
          skinny = 18.5
          normal = 25.0
          fat = 30.0
```

where 中定义的量只在本函数内可见。where 中可以使用模式匹配，上例可以改成：

```haskell
...  
where bmi = weight / height ^ 2  
      (skinny, normal, fat) = (18.5, 25.0, 30.0)
```

where 中可以定义函数，甚至可以嵌套

```haskell
calcBmis :: (RealFloat a) => [(a, a)] -> [a]  
calcBmis xs = [bmi w h | (w, h) <- xs] 
    where bmi weight height = weight / height ^ 2
```

---

## 柯里化和不全调用

```haskell
compareWithHundred :: (Num a, Ord a) => a -> Ordering
compareWithHundred = compare 100

divideByTen :: (Floating a) => a -> a
divideByTen = (/10)		-- 中缀函数的不全调用
```

## 高阶函数

```haskell
applyTwice :: (a -> a) -> a -> a  
applyTwice f x = f (f x)

zipWith' :: (a -> b -> c) -> [a] -> [b] -> [c]  
zipWith' _ [] _ = []  
zipWith' _ _ [] = []  
zipWith' f (x:xs) (y:ys) = f x y : zipWith' f xs ys

flip' :: (a -> b -> c) -> b -> a -> c  
flip' f y x = f x y
```

### map  filter  takeWhile

```haskell
sum (takeWhile (<10000) (filter odd (map (^2) [1..]))) -- 找出所有小于 10000，且同时为奇数和平方数的数字，求和
```

以 1 到 100 之间的所有数作为起始数，有多少 Collatz 序列的长度大于15

```haskell
collatzNext :: Integral a => a -> a
collatzNext x
  | odd x = 3 * x + 1
  | otherwise = div x 2

getCollatz :: Integral a => a -> [a]
getCollatz 1 = [1]
getCollatz x = x : getCollatz (collatzNext x)

length(filter (>15) (map length (map getCollatz [1..100])))
```

---

## lambda

匿名函数 `\参数 -> 表达式`.

```haskell
map (\x -> x*x) [1..10]
zipWith (\x y -> x*y) [1..10] [2..11]

-- 可以使用模式匹配
filter (\(x,y) -> x < y) [(x,y) | x <- [1..5], y <- [1..5]]
```

在 lambda 中使用模式匹配时仅能使用一个模式，匹配失败就会导致运行时错误

---

## $

```haskell
-- 定义
($) :: (a -> b) -> a -> b  
f $ x = f x

-- 用于替代括号
sqrt (1 + 2 + 3)
sqrt $ 1 + 2 + 3

-- 用于调用函数
map ($ 3) [(4+),(10*),(^2),sqrt]
```

---

## 函数复合

```haskell
-- 定义
(.) :: (b -> c) -> (a -> b) -> a -> c  
f . g = \x -> f (g x)

map (negate . abs) [5,-3,-6,7,-3,2,-19,24]

sum (takeWhile (<10000) (filter odd (map (^2) [1..])))
sum . takeWhile (<10000) . filter odd . map (^2) $ [1..]
```

