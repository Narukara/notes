## 用递归代替循环

技巧：将循环中所需的变量，转化成递归函数所用的参数

一种比较通用的编写思路：一般性递归

## 自然数公理

首先有后继 `succ` 和前驱 `prev` 函数。于是加法、减法、乘法、幂运算可以定义为：

```haskell
add 0 b = b
add a b = succ $ add (prev a) b

minus a 0 = a
minus a b = prev $ minus a (prev b)

times a 0 = 0
times a b = add a $ times a (prev b)

mi a 0 = 1
mi a b = times a $ mi a (prev b)
```

定义小于关系：

```haskell
less a 0 = False
less 0 b = True
less a b = less (prev a) (prev b)
```

在此基础上，可以定义整数除法：

```haskell
divd a b
  | less a b = 0
  | otherwise = succ $ divd (minus a b) b
```

## 停机问题

假设有函数 `willstop` 可以判断任意函数是否停机：

```haskell
willstop f = ...
```

若函数 `f` 可以停机，则返回 `True`，否则返回 `False`。

那么可以构造如下函数：

```haskell
f = willstop f && deadloop
  where
    deadloop = deadloop
```

不妨假设 `willstop f` 为 `True`，即 `f` 可以停机。那么在调用 `f` 时，它会陷入死循环 `deadloop`，无法停机，矛盾。

假设 `willstop f` 为 `False`，即 `f` 不能停机。那么在调用 `f` 时，由于短路求值，`deadloop` 不会被执行，直接返回结果，能够停机，矛盾。

因此 `willstop` 不可能被实现，停机问题不能被判定。

## Y 组合子

在定义递归函数时，通常来说我们不能将它定义为匿名函数，因为这个函数需要调用自身，也就需要一个名字。而在无类型 lambda 演算中我们无法给函数命名，为了实现递归函数就需要一个特殊的技巧，就是 Y 组合子。Y 组合子是一个函数，它作用在一个匿名函数上，可以将其转换成一个递归函数。

如果对于函数 $f$，有 $f\ g = g$，那么就说 $g$ 是 $f$ 的不动点。Y 组合子具有一个特殊的性质：$f\ (Y\ f)=(Y\ f)$，即它可以计算出函数的不动点。

haskell 中定义 Y 组合子：

```haskell
y f = let x = f x in x
```

递归形式的阶乘函数：

```haskell
fact 1 = 1
fact n = n * fact (n - 1)
```

去掉其中的递归，进而可以定义：

```haskell
fact' f 1 = 1
fact' f n = n * f (n - 1)
```

注意到 `fact` 是 `fact'` 的不动点。即 `fact' fact = fact`。于是将 Y 组合子作用到 `fact'` 上，就可以得到阶乘函数，即 `y fact' = fact`。
