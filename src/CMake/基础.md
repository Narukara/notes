CMake 自动生成的 Makefile 不仅可以通过 make 命令构建项目生成目标文件，还支持安装（make install）、测试安装的程序是否能正确执行（make test，或者ctest）、生成当前平台的安装包（make  package）、生成源码包（make package_source）、产生 Dashboard 显示数据并上传等高级功能。

## 命令行

1. 生成构建系统（生成 Makefile）

| 参数 | 含义                                                   |
| ---- | ------------------------------------------------------ |
| -S   | 指定源文件根目录，必须包含一个 CMakeLists.txt 文件     |
| -B   | 指定构建目录，构建生成的中间文件和目标文件的生成路径   |
| -D   | 指定变量，格式为-D \<var\>=\<value\>，-D后面的空格可以省略 |

```shell
cmake -B build -D CMAKE_BUILD_TYPE=Debug
```

2. 执行构建

可以直接在构建目录下使用 `make` 命令，或者使用 `cmake --build 构建目录` 执行构建，常用参数有

| 参数                   | 含义                                         |
| ---------------------- | -------------------------------------------- |
| --target               | 指定构建目标代替默认的构建目标，可以指定多个 |
| --parallel/-j [\<jobs\>] | 指定构建目标时使用的进程数                   |

## CMake 语法

CMake的命令有不同类型，包括**脚本命令、项目配置命令和测试命令**，细节可以查看官网[cmake-commands](https://link.zhihu.com/?target=https%3A//cmake.org/cmake/help/v3.20/manual/cmake-commands.7.html)。

CMake语言在项目配置中组织为三种源文件类型：

1. 目录：CMakeLists.txt，针对的是一个目录，描述如何针对目录（Source tree）生成构建系统，会用到项目配置命令；
2. 脚本：\<script\>.cmake，就是一个CMake语言的脚本文件，可使用`cmake -P`直接执行，只能包含脚本命令；
3. 模块：\<module\>.cmake，实现一些模块化的功能，可以被前面两者包含，比如`include(CTest)`启用测试功能。

### 注释

```cmake
# 注释
#[[
  注释块
  注释块
]]
```

### 变量

使用`set`和`unset`命令设置或者取消变量

1. 一般变量，可以是字符串，数字或者列表

```cmake
set(AUTHOR Tom)
set(AUTHOR "Tom Cat")
set(NUM 30)
set(ARR to be or)
set(ARR to;be;or)
# 引用一般变量：`${<variable>}`，在`if()`条件判断中可以简化为只用变量名`<variable>`
```

2. Cache 变量，一般用于提供配置选项，如果用户没有指定，则使用默认值

```cmake
# set(<variable> <value>... CACHE <type> <docstring> [FORCE])
set(CVAR "Default cache value" CACHE STRING "A sample for cache variable")
# 引用 CACHE 变量：$CACHE{<varialbe>}
# Cache 变量会被保存在构建目录下的 CMakeCache.txt 中，缓存起来之后是不变的，除非重新配置更新
```

3. 环境变量

```cmake
# 修改当前进程的环境变量，设置和引用格式为：
set(ENV{<variable>} [<value>])
$ENV{<varialbe>}
```

4. 内置变量

`CMAKE_`、`_CMAKE`或者以下划线开头后面加上任意 CMake 命令的变量。通过修改这些变量的值来配置 CMake 构建的行为

### 条件语句

1. 字符串比较，比如：STREQUAL、STRLESS、STRGREATER等；
2. 数值比较，比如：EQUAL、LESS、GREATER等；
3. 布尔运算，AND、OR、NOT；
4. 路径判断，比如：EXISTS、IS_DIRECTORY、IS_ABSOLUTE等；
5. 版本号判断；等等；
6. 使用小括号可以组合多个条件语句，比如：(cond1) AND (cond2 OR (cond3))。

对于常量：

1. ON、YES、TRUE、Y和非0值均被视为`True`；
2. 0、OFF、NO、FALSE、N、IGNORE、空字符串、NOTFOUND、及以"-NOTFOUND"结尾的字符串均视为`False`。

对于变量，只要其值不是常量中为`False`的情形，则均视为`True`。

```cmake
set(EMPTY_STR "")
if ((NOT EMPTY_STR) AND FLAG AND (NUM LESS 50) AND (NOT NOT_DEFINE_VAR))
    message("The first if branch...")
elseif (EMPTY_STR)
    message("EMPTY_STR is not empty")
else ()
    message("All other case")
endif()
```

### 消息打印

```cmake
message([<mode>] "message text" ...)
```

| 信息等级 \<mode\> | 说明                                                   |
| --------------- | ------------------------------------------------------ |
| 空或者`NOTICE`  | 比较重要的信息                                         |
| DEBUG           | 调试信息，主要针对开发者                               |
| STATUS          | 项目使用者可能比较关心的信息，比如提示当前使用的编译器 |
| WARNING         | CMake警告，不会打断进程                                |
| SEND_ERROR      | CMake错误，会继续执行，但是会跳过生成构建系统          |
| FATAL_ERROR     | CMake致命错误，会终止进程                              |

### 列表操作

`list` 命令用于列表操作，有很多子命令：

1. `APPEND`，往列表中添加元素；
2. `LENGTH`，获取列表元素个数；
3. `JOIN`，将列表元素用指定的分隔符连接起来；

### 文件操作

`file` 命令用于文件操作，有很多子命令，可以读写、创建或复制文件和目录、计算文件hash、下载文件、压缩文件、递归查找文件等

```cmake
# 查找 src/ 下所有 .c 文件
file(GLOB_RECURSE ALL_SRC src/*.c)
# 或者
aux_source_directory(src ALL_SRC)
```

### 配置文件生成

使用 `configure_file` 命令可以将 配置文件模板 中的特定内容替换，生成 目标文件。 输入文件中的`@VAR@`或者`${VAR}`在输出文件中将被对应的变量值替换。

例如有配置文件模板 `version.h.in`

```c
#define VERSION "@VERSION@"
```

执行 CMake 脚本

```cmake
set(VERSION 1.0.0)
configure_file(version.h.in "${PROJECT_SOURCE_DIR}/version.h")
```

得到 `version.h`

```c
#define VERSION "1.0.0"
```

### 执行系统命令

```cmake
execute_process(COMMAND bash "-c" "git rev-parse --short HEAD" OUTPUT_VARIABLE COMMIT_ID)
```

### 查找库文件

```cmake
find_library(<VAR> name1 [path1 path2 ...])
```

找到的库就可以被其他target使用，表明依赖关系。

### 导入其他模块

```cmake
include(CTest) # 将cmake文件或者模块加载并执行
```

CMake自带有很多有用的模块，官网链接：[cmake-modules](https://cmake.org/cmake/help/latest/manual/cmake-modules.7.html)

## 项目配置

### 设置项目名称&版本号

```cmake
project(CMakeTemplate VERSION 1.0.0 LANGUAGES C CXX)
```

CMake 会将版本号各值（major.minor.patch.tweak）分别赋值给以下变量

```cmake
PROJECT_VERSION, <PROJECT-NAME>_VERSION
PROJECT_VERSION_MAJOR, <PROJECT-NAME>_VERSION_MAJOR
PROJECT_VERSION_MINOR, <PROJECT-NAME>_VERSION_MINOR
PROJECT_VERSION_PATCH, <PROJECT-NAME>_VERSION_PATCH
PROJECT_VERSION_TWEAK, <PROJECT-NAME>_VERSION_TWEAK
```

配合 `configure_file` 命令，可以方便地在代码中得到版本信息

### 指定C/C++标准

```cmake
set(CMAKE_C_STANDARD 99)
set(CMAKE_CXX_STANDARD 11)
```

### 编译选项&编译类型

```cmake
add_compile_options(-Wall -Wextra -pedantic -Werror)		# 为所有编译器配置编译选项
set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pipe -std=c99")		# 为 c 编译器配置
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pipe -std=c++11")	# 为 c++ 编译器配置
```

通过设置变量`CMAKE_BUILD_TYPE`来配置编译类型，可设置为：`Debug`、`Release`、`RelWithDebInfo`、`MinSizeRel`。一般在执行`cmake`命令的时候通过参数`-D`指定

可以针对不同的编译类型设置不同的编译选项，例如：

```cmake
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -g -O0")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -O2")
```

### 宏定义

添加全局的宏定义

```cmake
add_definitions(-DDEBUG -DREAL_COOL_ENGINEER)
```

### 添加 include 目录

添加头文件的搜索目录

```cmake
include_directories(src/c)
```

### 设置编译目标

编译目标 (target) 的类型包括静态库、动态库和可执行文件

1. 编译为静态/动态库

使用命令 `add_library(库名称 STATIC 所有源文件)` 编译静态库，例如

```cmake
file(GLOB_RECURSE MATH_SRC components/math/*.c)
add_library(math STATIC ${MATH_SRC})
```

指定为`SHARED`则编译为动态库

2. 编译为可执行文件

```cmake
add_executable(demo main/main.c)	# (名称 所有源文件)
target_link_libraries(demo math)	# 指定依赖的库
```

### 安装&打包&测试

https://zhuanlan.zhihu.com/p/371257515

## 模块化构建

`CMakeLists.txt`定义一个目录的构建系统。模块化构建就是分别为每一个子模块目录编写一个`CMakeLists.txt`，在父目录中导入子目录的构建系统的生成目标，以便在父目录中使用。

1. 在子模块目录编写 `CMakeLists.txt`，目标设置为静态库
2. 在父目录中导入子模块

```cmake
add_subdirectory(components/math)	# 导入子模块
add_executable(demo main/main.c)	# 顶层目标
target_link_libraries(demo math)	# 指定依赖的库（子模块）
```

> 通常，用 add_subdirectory 导入的子模块在项目的子目录下，例如子模块在 `components/math` 下，那么编译产生的文件放在 `build/components/math` 下。如果子模块在项目外，就需要自行指定在 `build` 下存放的位置：
>
> ```cmake
> add_subdirectory(source_dir [binary_dir] [EXCLUDE_FROM_ALL])
> ```

另外，也可以直接导入已编译好的目标文件：

```cmake
add_library(math STATIC IMPORTED)
set_property(TARGET math PROPERTY IMPORTED_LOCATION "./lib/libmath.a")
```

